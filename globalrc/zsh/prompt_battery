#!/bin/zsh

# Generates battery info (based on grml, only for Linux)
update_battery() {
  battery_level=''
  if [[ $(uname -s) == 'Linux' ]]; then
    local batteries bat capacity battery_color i
    batteries=( /sys/class/power_supply/BAT*(N) )
    if (( $#batteries > 0 )) ; then
      for bat in $batteries ; do
        capacity=$(< $bat/capacity)
        case $(< $bat/status) in
        Charging)
          battery_level+=" $battery_charging"
          ;;
        Discharging)
          battery_level+=" $battery_discharging"
          ;;
        *) # Full, Unknown
          battery_level+=" "
          ;;
        esac
        if (( capacity < 20 )) ; then
          battery_color=$battery_color_low
        elif (( capacity < 45 )) ; then
          battery_color=$battery_color_medium
        else
          battery_color=$battery_color_high
        fi
        battery_level+=$battery_color
        i=10
        while (( i <= capacity )); do
          battery_level+=$battery_charged_character
          ((i+=10))
        done
        battery_level+=$battery_color_background
        while (( i <= 100 )); do
          battery_level+=$battery_discharged_character
          ((i+=10))
        done
      done
    fi
  fi
}
